plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id("maven-publish")
}

group = MavenConfig.GROUP
version = Version.INSTANCE.getCoreVersion()

afterEvaluate {
    publishing {
        repositories {
            String username = MavenConfig.INSTANCE.getUsername(project)
            String password = MavenConfig.INSTANCE.getPassword(project)
            if (!username.isEmpty() && !password.isEmpty()) {
                maven {
                    credentials {
                        setUsername(username)
                        setPassword(password)
                    }
                    url = uri(MavenConfig.INSTANCE.getRepoUrl(version as String))
                }
            } else {
                mavenLocal()
            }
        }

        publications {
            release(MavenPublication) {
                groupId MavenConfig.GROUP
                artifactId "core-render-android"
                version Version.INSTANCE.getRenderVersion()
                artifact(tasks.getByName("bundleReleaseAar"))
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                        // 添加 tdfcommon 依赖
                        if (dp.group == "com.tencent.tdf" && dp.name == "tdf-common") {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dp.group)
                            dependencyNode.appendNode('artifactId', dp.name)
                            dependencyNode.appendNode('version', dp.version)
                            // for exclusions
                            if (dp.excludeRules.size() > 0) {
                                def exclusions = dependencyNode.appendNode('exclusions')
                                dp.excludeRules.each { ExcludeRule ex ->
                                    def exclusion = exclusions.appendNode('exclusion')
                                    exclusion.appendNode('groupId', ex.group)
                                    exclusion.appendNode('artifactId', ex.module)
                                }
                            }
                        }
                    }
                }
            }
        }
    }

//    tasks.named("publishReleasePublicationToMavenRepository").configure {
//        dependsOn("bundleReleaseAar")
//    }
}


android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
//    kotlinOptions {
//        jvmTarget = '1.8'
//    }

    sourceSets {
        main.java.srcDirs(
                project.rootDir.absolutePath + "/core-render-android/src/main/java",
        )
        main.res.srcDirs(
                project.rootDir.absolutePath + "/core-render-android/src/main/res"
        )
    }
}

dependencies {
    api("com.tencent.tdf:tdf-common:1.0.1")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.11")
    compileOnly(project(":core"))

    compileOnly("com.android.support:recyclerview-v7:28.0.0")
    compileOnly("com.android.support:appcompat-v7:28.0.0")
    implementation("com.android.support:support-dynamic-animation:28.0.0")
}